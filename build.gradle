plugins {
    id "fabric-loom" version "1.10-SNAPSHOT"
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven {
        name = 'babbaj-repo'
        url = 'https://babbaj.github.io/maven/'
    }
    maven {
        name = "modrinth"
        url = "https://api.modrinth.com/maven"

        content {
            includeGroup "maven.modrinth"
        }
    }

    maven {
        name = "meteor-maven"
        url = uri("https://maven.meteordev.org/releases")
    }
    maven {
        name = "meteor-maven-snapshots"
        url = uri("https://maven.meteordev.org/snapshots")
    }
}

configurations {
    libImpl
    modImpl
}

dependencies {
    // Minecraft and Mappings
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    // Fabric API and Loader
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Rendering Mods
    modCompileOnly("maven.modrinth:sodium:${project.sodium_version}") { transitive = false }
    modCompileOnly("maven.modrinth:lithium:${project.lithium_version}") { transitive = false }
    modCompileOnly("maven.modrinth:iris:${project.iris_version}") { transitive = false }

    modImplementation("maven.modrinth:sodium:${project.sodium_version}")
    //modImpl("meteordevelopment:baritone:${project.baritone_version}-SNAPSHOT")

    // Custom Implementations
    implementation "dev.babbaj:nether-pathfinder:${project.nether_pathfinder_version}"

    // Library Dependencies
    libImpl("io.netty:netty-handler-proxy:${project.netty_version}") { transitive = false }
    libImpl("io.netty:netty-codec-socks:${project.netty_version}") { transitive = false }

    // Add libImpl dependencies to implementation
    configurations.libImpl.dependencies.each {
        implementation(it)
    }

    // Add modImpl dependencies to modImplementation and include them
    configurations.modImpl.dependencies.each {
        modImplementation(it)
        include(it)
    }
}

loom {
	accessWidenerPath = file("src/main/resources/aoba.accesswidener")
}
    
processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}


tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
    from {
        configurations.libImpl.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
